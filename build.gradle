plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm' version '1.9.0'
}

group = 'me.aagaming'
version = '0.1.3'


java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

repositories {
    mavenCentral()
    google()
}

dependencies {
    compileOnly 'com.android.tools.build:gradle:8.0.2'
}

publishing {
    publications {
        library(MavenPublication) {
            groupId = 'me.aagaming'
            artifactId = 'fast-load-plugin'

            from components.java

            pom {
                name = 'FTC Fast Load Plugin'
                description = 'Allows for hot swapping Team Code without restarting FtcRobotController'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://spdx.org/licenses/MIT.txt'
                    }
                }
                developers {
                    developer {
                        id = 'matthewo'
                        name = 'Matthew Oates'
                        email = 'uni.oates@gmail.com'
                    }
                    developer {
                        id = 'AAGaming00'
                        name = 'AAGaming'
                        email = 'aagaming@riseup.net'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/AAGaming00/fast-load-plugin"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

gradlePlugin {
    plugins {
        fastload {
            id = 'me.aagaming.fast-load-plugin'
            implementationClass = 'org.team11260.FastLoadPlugin'
            displayName = 'FastLoad'
            description = 'Allows for hot swapping Team Code without restarting FtcRobotController'
            tags.set(['ftc', 'java', 'kotlin'])
        }
    }

    automatedPublishing = false

    website = "https://github.com/AAGaming00/FtcFastLoadPlugin"
    vcsUrl = "https://github.com/AAGaming00/FtcFastLoadPlugin"
}
